; 主引导程序
; ----------------------------------------
section MBR vstart=0x7c00
    ; 设置段寄存器
    mov ax, cs
    mov ds, ax
    mov es, ax
    mov ss, ax
    mov fs, ax
    mov sp, 0x7c00

    ; 清屏 利用0x06号功能，上卷全部行，则可清屏。
    ; -----------------------------------------------------------
    ; INT 0x10   功能号:0x06	   功能描述:上卷窗口
    ; ------------------------------------------------------
    ; 输入:
    ;   AH 功能号= 0x06
    ;   AL = 上卷的行数(如果为0,表示全部)
    ;   BH = 上卷行属性
    ;   (CL,CH) = 窗口左上角的(X,Y)位置
    ;   (DL,DH) = 窗口右下角的(X,Y)位置
    ; 无返回值:
    mov ax, 0x06_00
    mov bx, 0x07_00
    mov cx, 0x00_00
    mov dx, 0x18_4f

    int 0x10

    ; 获取光标位置 利用0x03功能
    ; -----------------------------------------------------------
    ; INT 0x10   功能号:0x03	   功能描述:获得光标位置
    ; ------------------------------------------------------
    ; 输入:
    ;   AH 功能号= 0x03
    ;   BH = 待获取光标的页号
    ; 返回值:
    ;   (CH, CL) = 光标开始行, 光标结束行
    ;   (DH, DL) = 光标所在行号, 光标所在列号
    mov ah, 0x03
    mov bh, 0x00

    int 0x10


    ; 获取光标位置 利用0x13功能
    ; -----------------------------------------------------------
    ; INT 0x10   功能号:0x13	   功能描述:获得光标位置
    ; ------------------------------------------------------
    ; 输入:
    ;   AH 功能号= 0x13
    ;   AL = 写字符方式, AL=0x01表示光标跟随字符移动(每写一个字符，光标后移一个字符)
    ;   BH = 要显示的页号
    ;   BL = 字符属性, BL=0x02表示黑底绿字
    ;   CX = 字符串的长度，不包括结尾的'\0'
    ;   ES:BP = 字符串首地址
    ; 无返回值:
    mov ax, message
    mov bp, ax
    mov cx, 5
    mov ax, 0x13_01
    mov bx, 0x00_02

    int 0x10

    ; 程序悬停在此
    jmp $

    message db "1 MBR"
    times 510 - ($ - $$) db 0
        db 0x55, 0xaa
